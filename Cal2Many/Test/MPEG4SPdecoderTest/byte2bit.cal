

// byte2bit.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)

// Read in bytes and shift out serially in msb-first order.

import all caltrop.lib.BitOps;

actor byte2bit () int(size=8) in8 ==> bool out :

  bool _CAL_tokenMonitor := true;
  
  int(size=5) count := 0;
  int(size=9) buf;

  // Read in an new byte when needed
  reload: action in8:[i] ==>
  guard count = 0
    do
    buf := i;
    count := 8;
  end

  // Write out bits in msb first order
  shift: action ==> out:[ bit ]
  guard
    count != 0
  var
    bool bit = bitand( buf, 128) != 0
  do
    count := count - 1;
    buf := lshift( buf, 1 );
  end

  priority
    reload > shift;
  end
end